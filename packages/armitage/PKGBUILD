# Maintainer: Alan Placidina Maria <alanplacidina[at]gmail[dot]com>
# pls note, this is a binary package.

# please note systemd .service file is experimental. If anyone knows how to
# improve, feel free to drop me a line

pkgname=armitage
pkgver=20220123
pkgrel=4
_java=8
pkgdesc="Metasploit GUI Front ened written in java"
url="http://www.fastandeasyhacking.com/"
arch=('any')
license=('GPL')
depends=('metasploit' 'java-environment')
makedepends=('gradle' "java-environment=${_java}")
conflicts=('armitage-svn')
install='armitage.install'
source=("https://gitlab.com/kalilinux/packages/armitage/-/archive/upstream/${pkgver}/armitage-upstream-${pkgver}.tar.bz2"
        'armitage.install'
        'armitage.desktop'
        'armitage.png'
        'metasploit.sh')
sha256sums=('SKIP'
            '66060dc1078a47c9ad58a18b1ca6393d1d991ba2c6a4ddfe7c8d2065c2051eb7'
            '5fbae6cca85a4f4d670c1b26efe549447685f4811a6c62228b42fa59a94df6f9'
            '73c73ad6c803a574f9c8cf31f140dc758f5428cf55bc74f9d11c93a483975141'
            '7869413529c5529b9a0f2bcaaa2be1480382c2f91eb505e43a4f9e1c1eab2d71')

prepare() {
  sed -E "s|^(Exec=).*|\1env MSF_DATABASE_CONFIG=$HOME/.msf4/database.yml armitage|g" -i armitage.desktop
}

build() {
  cd "${pkgname}-upstream-${pkgver}"
  export PATH="/usr/lib/jvm/java-${_java}-openjdk/bin:$PATH"
  export JAVA_HOME="/usr/lib/jvm/java-${_java}-openjdk"
  ./package.sh
}

package() {
  cd "${pkgname}-upstream-${pkgver}/release/unix"

  cat > armitage << EOF
#!/bin/sh
cd /opt/armitage
java -XX:+AggressiveHeap -XX:+UseParallelGC -jar armitage.jar $@
EOF

  mkdir -p "${pkgdir}/opt/${pkgname}"

  cp "${pkgname}.jar" "${pkgdir}/opt/${pkgname}/"
  cp "cortana.jar" "${pkgdir}/opt/${pkgname}/"
  cp "teamserver" "${pkgdir}/opt/${pkgname}/"

  install -Dm644 "${srcdir}/armitage.desktop" "${pkgdir}/usr/share/applications/armitage.desktop"
  install -Dm644 "${srcdir}/armitage.png" "${pkgdir}/usr/share/icons/armitage.png"
  install -Dm755 "${srcdir}/metasploit.sh" "${pkgdir}/etc/profile.d/metasploit.sh"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
